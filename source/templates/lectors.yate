module "lectors"

include "util.yate"

match / {
	if (.single) {
		apply .lectors
	} else {
		header("Список лекторов")
		for .lectors {
			apply .
		}
	}
}

match .lectors {
	lector_id = ._id.*[name() == "$oid"]
	<div class="lector post" data-cid="{.cid}">
		<div class="left">
			<img class="avatar fold-handle">
				if (.expanded) {
					@width = 250
					@src = .photo_url
				} else {
					@height = 50
					@width = 50
					@src = .preview_url
				}
			</img>
		</div>
		<div class="content">
			<div class="caption-wrapper">
				<div class="caption fold-handle">{.first_name} {.last_name}</div>
			</div>
			if (.expanded) {
				if (.about) {
					<hr/>
					<p>{.about}</p>
				}
				lector_lectures = ..lectures //[.lector_id == lector_id]
				if (lector_lectures) {
					<hr/>
					<div class="caption-wrapper">
						<div class="caption">Лекции:</div>
					</div>
					for lector_lectures {
						<p>
							"{.name}: "
							<a href="{.url}">{.url}</a>
						</p>
					}
				}
			}			
		</div>
	</div>
}